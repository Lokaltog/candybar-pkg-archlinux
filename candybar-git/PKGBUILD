# -*- mode: sh -*-
# Maintainer: Kim Silkeb√¶kken <kim.silkebaekken+aur@gmail.com>

_gitname=candybar
_gitbranch=develop
pkgname="${_gitname}-git"
pkgdesc="WebKit-based status bar for tiling window managers"
pkgver=367.d5866e6
pkgrel=1
url="https://github.com/Lokaltog/${_gitname}"
license=("MIT")
arch=("i686" "x86_64")
depends=("gtk3"
         "webkitgtk"
         "jansson")
optdepends=("alsa-lib: volume widget"
            "curl: external ip and weather widgets"
            "graphicsmagick: transparent background with effects"
            "libdbus: notification daemon and battery status widgets"
            "libmpdclient: now playing/mpd widget"
            "libxcb: desktops and window title widgets"
            "playerctl: MPRIS media player widget"
            "xcb-util-wm: desktops and window title widgets")
makedepends=("git" "python")
backup=("etc/xdg/candybar/config.json")
install="${_gitname}.install"
source=("${_gitname}::git://github.com/Lokaltog/${_gitname}.git#branch=${_gitbranch}"
        "${install}")
sha256sums=("SKIP"
            "85d9be3c86bdb40ce3cdcd374a83768caaec535726a80c8944a743da3192e177")

pkgver() {
    cd "${_gitname}"
    echo "$(git rev-list --count ${_gitbranch}).$(git rev-parse --short ${_gitbranch})"
}

build() {
    cd "${_gitname}"
    ./waf configure build --prefix="/usr"
}

package() {
    cd "${_gitname}"
    ./waf install --destdir="${pkgdir}"
    install -Dm644 "docs/candybar.1" "${pkgdir}/usr/man/man1/candybar.1"
    install -Dm644 "docs/candybar.5" "${pkgdir}/usr/man/man5/candybar.5"
    install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
