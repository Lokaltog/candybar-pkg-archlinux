# Maintainer: Kim Silkeb√¶kken <kim.silkebaekken+aur@gmail.com>

_gitname=wkline
_gitbranch=develop
pkgname="${_gitname}-git"
pkgdesc='WebKit-based status line for tiling window managers.'
pkgver=81.fe3ad3a
pkgrel=1
url="https://github.com/Lokaltog/${_gitname}"
license=('MIT')
arch=('x86_64')
depends=('gtk3' 'webkitgtk' 'jansson' 'libxcb' 'xcb-util-wm' 'curl' 'libmpdclient')
makedepends=('git')
install="${_gitname}.install"
source=("${_gitname}::git://github.com/Lokaltog/${_gitname}.git#branch=${_gitbranch}"
        "${install}")
sha256sums=('SKIP'
            '9103b23caf0427f2e88a2b1538d85b4a617e4a275cd17188a852d33aaffa0446')

pkgver() {
	cd "${_gitname}"
	echo "$(git rev-list --count ${_gitbranch}).$(git rev-parse --short ${_gitbranch})"
}

build() {
	cd "${_gitname}"
	autoreconf --install
	./configure --prefix=/usr
	make
}

package() {
	cd "${_gitname}"
	make DESTDIR="${pkgdir}" install
	install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
