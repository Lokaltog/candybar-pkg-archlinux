# Maintainer: Kim Silkeb√¶kken <kim.silkebaekken+aur@gmail.com>

_gitname=wkline
_gitbranch=develop
pkgname="${_gitname}-git"
pkgdesc='WebKit-based status line for tiling window managers.'
pkgver=146.8ac7492
pkgrel=1
url="https://github.com/Lokaltog/${_gitname}"
license=('MIT')
arch=('i686' 'x86_64')
depends=(
	'gtk3'
	'webkitgtk'
	'jansson')
optdepends=(
	'alsa-lib: volume widget'
	'curl: external ip and weather widgets'
	'libdbus: notification daemon and battery status widgets'
	'libmpdclient: now playing/mpd widget'
	'libxcb: desktops and window title widgets'
	'xcb-util-wm: desktops and window title widgets')
makedepends=('git')
install="${_gitname}.install"
source=("${_gitname}::git://github.com/Lokaltog/${_gitname}.git#branch=${_gitbranch}"
        "${install}")
sha256sums=('SKIP'
            '94f6d39e863e5560cf697e7c91cd190734d87cb8962edda1819a0078f454aa99')

pkgver() {
	cd "${_gitname}"
	echo "$(git rev-list --count ${_gitbranch}).$(git rev-parse --short ${_gitbranch})"
}

build() {
	cd "${_gitname}"
	autoreconf --install
	./configure --prefix=/usr
	make
}

package() {
	cd "${_gitname}"
	make DESTDIR="${pkgdir}" install
	install -Dm644 "config.def.json" "${pkgdir}/usr/share/wkline/config.def.json"
	install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
